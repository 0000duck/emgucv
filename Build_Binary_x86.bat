@echo off
IF "%1%"=="64" ECHO "BUILDING 64bit solution" 
IF NOT "%1%"=="64" ECHO "BUILDING 32bit solution"

SET OS_MODE=
IF "%1%"=="64" SET OS_MODE= Win64
  
SET PROGRAMFILES_DIR=%programfiles(x86)%
if NOT EXIST "%PROGRAMFILES_DIR%" SET PROGRAMFILES_DIR=%programfiles%

REM Find CMake  
SET CMAKE="cmake.exe"
IF EXIST "%PROGRAMFILES_DIR%\CMake 2.8\bin\cmake.exe" SET CMAKE="%PROGRAMFILES_DIR%\CMake 2.8\bin\cmake.exe"

IF EXIST "CMakeCache.txt" del CMakeCache.txt

SET CMAKE_CONF_FLAGS= -DBUILD_DOXYGEN_DOCS:BOOL=FALSE -DBUILD_TESTS:BOOL=FALSE -DBUILD_NEW_PYTHON_SUPPORT:BOOL=FALSE -DOPENCV_WHOLE_PROGRAM_OPTIMIZATION:BOOL=TRUE -DEMGU_ENABLE_SSE:BOOL=TRUE 

IF "%2%"=="intel" GOTO INTEL_COMPILER
IF NOT "%2%"=="intel" GOTO VISUAL_STUDIO

:INTEL_COMPILER
REM Find Intel Compiler 
SET INTEL_DIR=%PROGRAMFILES_DIR%\Intel\ComposerXE-2011\bin
SET INTEL_ENV=%INTEL_DIR%\iclvars.bat
SET INTEL_ICL=%INTEL_DIR%\ia32\icl.exe
SET INTEL_TBB=%INTEL_DIR%\..\tbb\include\tbb

REM initiate the compiler enviroment
@echo on
IF "%OS_MODE%"=="" CALL "%INTEL_ENV%" ia32
IF "%OS_MODE%"==" WIN64" CALL "%INTEL_ENV%" intel64

SET INTEL_ICL_CMAKE=%INTEL_ICL:\=/%
SET INTEL_TBB_CMAKE=%INTEL_TBB:\=/%

SET INTEL_ICL_COMPILER_FLAGS="/STACK:10000000 /INCREMENTAL:NO /machine:X86"
SET INTEL_ICL_LINKER_FLAGS="/QaxSSE4.1 /arch:IA32 /Qparallel /Quse-intel-optimized-headers /EHa"

cmake ^
-G "NMake Makefiles" ^
-DMSVC:BOOL=TRUE ^
-DMSVC10:BOOL=TRUE ^
-DCMAKE_C_COMPILER="%INTEL_ICL_CMAKE%" ^
-DCMAKE_CXX_COMPILER="%INTEL_ICL_CMAKE%" ^
-DCMAKE_EXE_LINKER_FLAGS=%INTEL_ICL_COMPILER_FLAGS% ^
-DCMAKE_SHARED_LINKER_FLAGS=%INTEL_ICL_COMPILER_FLAGS% ^
-DUSE_O3:BOOL=TRUE ^
-DCMAKE_CXX_FLAGS=%INTEL_ICL_LINKER_FLAGS% ^
-DCMAKE_C_FLAGS=%INTEL_ICL_LINKER_FLAGS% ^
-DWITH_TBB:BOOL=TRUE ^
-DTBB_INCLUDE_DIR="%INTEL_TBB_CMAKE%" ^
-DUSE_IPP:BOOL=TRUE ^
%CMAKE_CONF_FLAGS%

nmake

GOTO END

:VISUAL_STUDIO
REM Find Visual Studio or Msbuild
SET VS2005="%PROGRAMFILES_DIR%\Microsoft Visual Studio 8\Common7\IDE\devenv.exe"
SET VS2008="%PROGRAMFILES_DIR%\Microsoft Visual Studio 9.0\Common7\IDE\devenv.exe"
SET VS2010="%PROGRAMFILES_DIR%\Microsoft Visual Studio 10.0\Common7\IDE\devenv.exe"
SET MSBUILD35="%windir%\Microsoft.NET\Framework\v3.5\MSBuild.exe"

IF EXIST %MSBUILD35% SET DEVENV=%MSBUILD35%
IF EXIST %VS2005% SET DEVENV=%VS2005% 
IF EXIST %VS2008% SET DEVENV=%VS2008%
IF EXIST %VS2010% SET DEVENV=%VS2010%

IF %DEVENV%==%MSBUILD35% SET BUILD_TYPE=/property:Configuration=Release
IF %DEVENV%==%VS2005% SET BUILD_TYPE=/Build Release
IF %DEVENV%==%VS2008% SET BUILD_TYPE=/Build Release
IF %DEVENV%==%VS2010% SET BUILD_TYPE=/Build Release

IF %DEVENV%==%MSBUILD35% SET CMAKE_CONF="Visual Studio 8 2005%OS_MODE%"
IF %DEVENV%==%VS2005% SET CMAKE_CONF="Visual Studio 8 2005%OS_MODE%"
IF %DEVENV%==%VS2008% SET CMAKE_CONF="Visual Studio 9 2008%OS_MODE%"
IF %DEVENV%==%VS2010% SET CMAKE_CONF="Visual Studio 10%OS_MODE%"

@echo on

%CMAKE% -G %CMAKE_CONF% %CMAKE_CONF_FLAGS% -DUSE_IPP:BOOL=FALSE .  
%DEVENV% %BUILD_TYPE% emgucv.sln

:END